# Open to More Info
# This version of the script has multiple repeat loops at the end:
# Loop 1 - click "More Info" to bring up the list of updates if possible
# Loop 2 - Click "Install Now" to begin the download/install and hold at EULA or User credential ask
# Validated in macOS 11, 12, 13, 14, 15 Intel & ARM
# September 2024 - validated against 14+Upgrade Banner; 15.0 RC to 15.1 Beta 1

# Get Major OS
set _major to system attribute "sys1"

# Bailout if old version
if _major < 11 then
	log "Catalina or earlier detected"
	error number -128
end if

# Simple launcher - current OS only
if (_major > 14) then
	log "Newest OS (Sequoia) detected"
	
	# Launch software Update preference pane
	do shell script "open x-apple.systempreferences:com.apple.Software-Update-Settings.extension"
	
	# Wait for window to open
	tell application "System Events"
		repeat 60 times
			if exists (window 1 of process "System Settings") then
				delay 3
				exit repeat
			else
				delay 1
			end if
		end repeat
		
		if not (exists (window 1 of process "System Settings")) then
			return
		end if
	end tell
	
	# Click "Update Now" or "Restart Now" if present
	tell application "System Events"
		tell process "System Settings"
			repeat 60 times
				
				# Sonoma logic - interface now has three buttons. If 2 buttons exist, use old logic, if three exist, use new logic. 
				
				# 3-button logic - if button 3 exists (indicating both "Update Tonight" and "Restart Now" buttons are present) , click button 2 for "Restart Now"
				if exists (button 3 of group 2 of scroll area 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events") then
					click button 2 of group 2 of scroll area 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events"
					exit repeat
				end if
				
				# 2-button logic ("Update Now" and "More Info...")	- click "Update Now", because as of 14.1 beta 3, the "More Info" button is still not accepting synthetic click commands			
				if exists (button 1 of group 2 of scroll area 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events") then
					click button 1 of group 2 of scroll area 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events"
					exit repeat
				end if
				# By this point we should now be at the EULA for the user to accept and then enter their password
				tell application "System Events"
					if application process "System Settings" exists then
						delay 0.5
					else
						exit repeat
					end if
				end tell
				
				delay 1
			end repeat
			
		end tell
	end tell
	
end if

####
# Sonoma
if (_major = 14) then
	log "Sonoma detected"
	# New bugs in Sonoma - More Info button still not actionable through 14.6.1. If Sonoma detected, click Upgrade now as far as you can, BUT if macOS upgrade banner detected, just scroll to the bottom
	
	# Launch software Update preference pane
	do shell script "open x-apple.systempreferences:com.apple.Software-Update-Settings.extension"
	
	# Wait for window to open
	tell application "System Events"
		repeat 60 times
			if exists (window 1 of process "System Settings") then
				delay 3
				exit repeat
			else
				delay 1
			end if
		end repeat
		
		if not (exists (window 1 of process "System Settings")) then
			return
		end if
	end tell
	
	# Click "Update Now" or "Restart Now" if present
	tell application "System Events"
		tell process "System Settings"
			repeat 60 times
				
				# If macOS Banner detected along with available update, all we can do is scroll to it, we cannot click further. Do this and bail out
				set uiElems to entire contents of group 2 of splitter group 1 of group 1 of window "Software Update"
				#uiElems is generated as a LIST
				
				set var2 to "Sequoia"
				
				log "Now checking for Sequoia string"
				repeat with i in uiElems
					if class of i is static text then
						set R to value of i
						log "Value of static text is"
						log R
						if R contains "Sequoia" then
							log "Seqoia detected"
							if exists (scroll bar 1 of scroll area 1 of group 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events") then
								
								set value of scroll bar 1 of scroll area 1 of group 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events" to 1.0
							end if
							display dialog "To start your update, look for the 'Also available' section at the bottom of this window and click the 'More Info...' button to begin the update." with title "Click the 'More Info' button to continue" buttons {"OK"} with icon POSIX file "/System/Library/PrivateFrameworks/OAHSoftwareUpdate.framework/Versions/A/Resources/SoftwareUpdate.icns" default button {"OK"}
							error number -128
						end if
					end if
				end repeat
				
				# Sequoia banner not detected, executing standard logic
				if exists (button 1 of group 2 of scroll area 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events") then
					click button 1 of group 2 of scroll area 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events"
					exit repeat
				end if
				
				tell application "System Events"
					if application process "System Settings" exists then
						delay 0.5
					else
						exit repeat
					end if
				end tell
				
				delay 1
			end repeat
		end tell
	end tell
	
end if


# Ventura
if (_major = 13) then
	log "Ventura detected"
	# New bugs in Ventura - More Info button still not actionable through 13.6.1. If Ventura detected, click Upgrade now as far as you can, BUT if macOS upgrade banner detected, just scroll to the bottom
	
	# Launch software Update preference pane
	do shell script "open x-apple.systempreferences:com.apple.Software-Update-Settings.extension"
	
	# Wait for window to open
	tell application "System Events"
		repeat 60 times
			if exists (window 1 of process "System Settings") then
				delay 3
				exit repeat
			else
				delay 1
			end if
		end repeat
		
		if not (exists (window 1 of process "System Settings")) then
			return
		end if
	end tell
	
	# Click "Update Now" or "Restart Now" if present
	tell application "System Events"
		tell process "System Settings"
			repeat 60 times
				
				# If macOS Banner detected along with available update, all we can do is scroll to it, we cannot click further. Do this and bail out
				set uiElems to entire contents of group 2 of splitter group 1 of group 1 of window "Software Update"
				#uiElems is generated as a LIST
				
				set var2 to "Sonoma"
				
				log "Now checking for Sonoma string"
				repeat with i in uiElems
					if class of i is static text then
						set R to value of i
						log "Value of static text is"
						log R
						if R contains "Sonoma" then
							log "Sonoma detected"
							if exists (scroll bar 1 of scroll area 1 of group 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events") then
								
								set value of scroll bar 1 of scroll area 1 of group 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events" to 1.0
							end if
							display dialog "To start your update, look for the 'Other Updates Available' section at the bottom of this window and click the 'More Info...' button to begin the update." with title "Click the 'More Info' button to continue" buttons {"OK"} with icon POSIX file "/System/Library/PrivateFrameworks/OAHSoftwareUpdate.framework/Versions/A/Resources/SoftwareUpdate.icns" default button {"OK"}
							error number -128
						end if
					end if
				end repeat
				
				# Sonoma banner not detected, executing standard logic
				if exists (button 1 of group 2 of scroll area 1 of group 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events") then
					click button 1 of group 2 of scroll area 1 of group 1 of group 1 of group 2 of splitter group 1 of group 1 of window "Software Update" of application process "System Settings" of application "System Events"
					exit repeat
				end if
				
				tell application "System Events"
					if application process "System Settings" exists then
						delay 0.5
					else
						exit repeat
					end if
				end tell
				
				delay 1
			end repeat
		end tell
	end tell
	
end if

# Monterey and Big Sur
if _major < 13 then
	log "Monterey or Big Sur detected"
	do shell script "open /System/Library/PreferencePanes/SoftwareUpdate.prefPane"
	
	# Launch System Preferences
	tell application "System Events"
		repeat 60 times
			if exists (window 1 of process "System Preferences") then
				exit repeat
			else
				delay 1
			end if
		end repeat
		
		if not (exists (window 1 of process "System Preferences")) then
			return
		end if
		
		tell application id "com.apple.systempreferences"
			set the current pane to pane id "com.apple.preferences.softwareupdate"
		end tell
	end tell
	
	tell application "System Events"
		tell process "System Preferences"
			
			# Click More Info button
			repeat 60 times
				if exists (button 1 of group 1 of window "Software Update") then
					click button 1 of group 1 of window "Software Update"
					exit repeat
				end if
				
				tell application "System Events"
					if application process "System Preferences" exists then
						delay 0.5
					else
						exit repeat
					end if
				end tell
				delay 1
			end repeat
			# End Click More Info button
			
			# Wait for checkboxes to appear
			repeat 60 times
				if exists (checkbox 1 of UI element 1 of row 1 of table 1 of scroll area 2 of sheet 1 of window "Software Update" of application process "System Preferences" of application "System Events") then
					exit repeat
				else
					delay 1
				end if
			end repeat
			
			# Ensure all checkboxes checked
			# Checkbox 1 (must exist)
			if not (value of checkbox 1 of UI element 1 of row 1 of table 1 of scroll area 2 of sheet 1 of window "Software Update" of application process "System Preferences" of application "System Events" as boolean) then
				click checkbox 1 of UI element 1 of row 1 of table 1 of scroll area 2 of sheet 1 of window "Software Update" of application process "System Preferences" of application "System Events"
			end if
			
			# Checkbox 2 (check and act if exists)
			if exists (checkbox 1 of UI element 1 of row 2 of table 1 of scroll area 2 of sheet 1 of window "Software Update" of application process "System Preferences" of application "System Events") then
				if not (value of checkbox 1 of UI element 1 of row 2 of table 1 of scroll area 2 of sheet 1 of window "Software Update" of application process "System Preferences" of application "System Events" as boolean) then
					click checkbox 1 of UI element 1 of row 2 of table 1 of scroll area 2 of sheet 1 of window "Software Update" of application process "System Preferences" of application "System Events"
				end if
			end if
			
			# Checkbox 3 (check and act if exists)
			if exists (checkbox 1 of UI element 1 of row 3 of table 1 of scroll area 2 of sheet 1 of window "Software Update" of application process "System Preferences" of application "System Events") then
				if not (value of checkbox 1 of UI element 1 of row 3 of table 1 of scroll area 2 of sheet 1 of window "Software Update" of application process "System Preferences" of application "System Events" as boolean) then
					click checkbox 1 of UI element 1 of row 3 of table 1 of scroll area 2 of sheet 1 of window "Software Update" of application process "System Preferences" of application "System Events"
				end if
			end if
			
		end tell
	end tell
	
end if
